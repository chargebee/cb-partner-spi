import groovy.json.JsonSlurper
import org.openapitools.generator.gradle.plugin.tasks.ValidateTask
import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id "idea"
    id "java"
    id 'maven-publish'
    id "org.openapi.generator" version "7.0.1"
    id 'net.researchgate.release' version '3.0.2'
}

group build_group
version build_version

sourceSets {
    main.resources.srcDirs = ['spec/capabilities']
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = "8"
    targetCompatibility = "8"
}



def loadSpecConfig() {
    def configJson = "[" + file('spec.config').text + "]"

    def config = new JsonSlurper().parseText(configJson)

    project.ext.specConfig = config as ArrayList
}

tasks.register("generateSpec")

tasks.register("validateSpec")

def openApiTask() {
    loadSpecConfig()

    for (int i = 0; i < specConfig.size(); i++) {
        def spec = specConfig[i]

        def validateTaskName = "validateSpec_$spec.name"

        def generateTaskName = "generateSpec_$spec.name"

        tasks.register(validateTaskName, ValidateTask) {
            inputSpec.set("$rootDir/spec/spi/openapi_$spec.name"+".yml")
            recommend.set(true)
        }

        tasks.register(generateTaskName, GenerateTask) {
            generatorName.set("java")
            inputSpec.set("$rootDir/spec/spi/openapi_$spec.name"+".yml")
            outputDir.set("$rootDir/generated/$spec.name")
            apiPackage.set("org.chargebee.spi.$spec.name"+".api")
            invokerPackage.set("org.chargebee.spi.$spec.name"+".client")
            modelPackage.set("org.chargebee.spi.$spec.name"+".model")
            groupId.set(project.group)
            id.set(spec.projectName)
            version.set(project.version)
            configFile.set("$rootDir/spec/spi/config.yml")
        }

        validateSpec.dependsOn(validateTaskName)
        generateSpec.dependsOn(generateTaskName)

        java.sourceSets["main"].java.srcDir("$rootDir/generated/$spec.name/src/main/java")
    }
}

openApiTask();

tasks.openApiGenerate.configure {
    actions.clear()
    dependsOn(generateSpec)
}

tasks.openApiValidate.configure {
    actions.clear()
    dependsOn(validateSpec)
}

release {
    // Configure release parameters
    failOnSnapshotDependencies = true

    // Custom tasks before release
    preTagCommitMessage = 'Pre tag commit: '
    tagCommitMessage = 'Creating tag: '
    newVersionCommitMessage = 'New version commit: '
}

publishing {
    repositories {
        maven {
            url = uri("${project.rootDir}/local-maven-repo")
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

tasks.register("releaseClean") {
    doLast {
        // Clean up release-related files
        delete(fileTree(project.rootDir) {
            include("release.properties", "gradle.properties.backup")
        })
    }
}

// Ensure publish task runs after release
afterReleaseBuild.dependsOn(tasks.publish)


//As of now we have to keep following dependencies to generate build with generated sources
dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "com.google.code.findbugs:jsr305:3.0.2"
    implementation 'io.gsonfire:gson-fire:1.8.5'
    implementation 'javax.ws.rs:jsr311-api:1.1.1'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.15.0'
    implementation 'org.apache.commons:commons-lang3:3.13.0'
    implementation 'jakarta.annotation:jakarta.annotation-api:1.3.5'
    testImplementation 'org.mockito:mockito-core:5.6.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
<<<<<<< HEAD
    // Add equivalent dependencies from POM
    implementation 'org.json:json:20231013'
    implementation 'commons-codec:commons-codec:1.16.0'
=======
>>>>>>> origin/main
}
